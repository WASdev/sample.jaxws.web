apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'liberty'

group = 'net.wasdev.wlp.sample.jaxws'
version = '1.0-SNAPSHOT'

description = "Jaxws Web Sample"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.6.3'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-javaee7', version: '17.0.0.2'

    compile 'com.sun.xml.ws:jaxws-ri:2.3.2'
    providedCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    providedCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    providedCompile group: 'jstl', name: 'jstl', version:'1.2'
}

//The HandlerChain class expects the Handler files in the classes folder.
//Because of this the resources directory needs to be merged with the classes directory
//so the files are in the correct location.
sourceSets {
    main {
        output.resourcesDir = 'build/classes/java/main'
    }
}

war {
    archiveName = baseName + '.' + extension
}

liberty {
    server {
        packageLiberty {
            archive = "$buildDir/sample.jaxws.web.zip"
            include = 'usr'
        }
        configFile = file('src/main/liberty/config/server.xml')
        bootstrapProperties = ['appLocation':war.archiveName]
        apps = [war]
    }
}

task printServerMessage {
    doLast {
        println 'The server is running at http://localhost:9131/jaxws/'
    }
}

check.dependsOn 'libertyStart'
libertyStart.finalizedBy 'printServerMessage'
